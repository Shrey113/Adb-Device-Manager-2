; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "adb devices manager"
#define MyAppVersion "1.2"
#define MyAppPublisher "ADB Devices Manager"
#define MyAppURL "https://shrey113.github.io/Adb-Device-Manager-2/"
#define MyAppExeName "adb_devices.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AE28C806-FDFE-4B32-91CB-ED2BDA583696}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
; Force the installer to run as Admin immediately (UAC prompt at start)
PrivilegesRequired=admin
OutputDir=A:\All Android\_main\adb_devices\init
OutputBaseFilename=adb_devices
SetupIconFile=C:\_add_by_me\~icon_s\android.ico
Compression=lzma2/ultra64
InternalCompressLevel=ultra
LZMAUseSeparateProcess=yes
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\All helper\*"; DestDir: "{app}\All helper"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\charset_converter_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\data\*"; DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\flutter_tts_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\flutter_windows.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\hotkey_manager_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\keypress_simulator_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\local_notifier_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\screen_retriever_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\system_tray_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\url_launcher_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "A:\All Android\_main\adb_devices\build\windows\x64\runner\Release\window_manager_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files




[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]

// Adds a firewall rule for a specific EXE
procedure AddFirewallRule(ExePath: string);
var
  ResultCode: Integer;
  Params: string;
begin
  // Name format: "adb devices manager - <FileName>"
  Params :=
    'advfirewall firewall add rule ' +
    'name="adb devices manager - ' + ExtractFileName(ExePath) + '" ' +
    'dir=in action=allow ' +
    'program="' + ExePath + '" enable=yes';

  Exec('netsh', Params, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
end;

// Removes a firewall rule for a specific EXE
procedure RemoveFirewallRule(ExePath: string);
var
  ResultCode: Integer;
  Params: string;
begin
  Params :=
    'advfirewall firewall delete rule ' +
    'name="adb devices manager - ' + ExtractFileName(ExePath) + '" ' +
    'program="' + ExePath + '"';
    
  Exec('netsh', Params, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
end;

// Recursively scans the directory and applies action
procedure ProcessFirewallRules(BaseDir: string; IsInstalling: Boolean);
var
  FindRec: TFindRec;
  FilePath: string;
begin
  if FindFirst(BaseDir + '\*', FindRec) then
  begin
    try
      repeat
        if (FindRec.Attributes and FILE_ATTRIBUTE_DIRECTORY) <> 0 then
        begin
          if (FindRec.Name <> '.') and (FindRec.Name <> '..') then
            ProcessFirewallRules(BaseDir + '\' + FindRec.Name, IsInstalling);
        end
        else if LowerCase(ExtractFileExt(FindRec.Name)) = '.exe' then
        begin
          FilePath := BaseDir + '\' + FindRec.Name;
          if IsInstalling then
            AddFirewallRule(FilePath)
          else
            RemoveFirewallRule(FilePath);
        end;
      until not FindNext(FindRec);
    finally
      FindClose(FindRec);
    end;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    // Scan {app} and ADD rules
    ProcessFirewallRules(ExpandConstant('{app}'), True);
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
    // Scan {app} and REMOVE rules (before files are deleted)
    ProcessFirewallRules(ExpandConstant('{app}'), False);
  end;
end;

